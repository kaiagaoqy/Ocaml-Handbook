## 1. `Let` -- Naming Values
#### 1.1 `Let` variant can give names to values in **local** and **global** syntexs

``` ocaml
let name = expr1 in expr2;;
(* give expr1 the name within expr2*)
```
Local defination: name is only available in expr2, or we can say that the name is local to the body of `let`

examples:
```ocaml
 # let n = 2 in n*n;; (* int -> int *)
- : int = 4
```
One important difference from the other languages is that *the entire `Let` defination itself is an expression, where should return a value or function*

**EXPRESSION: EVERYTHING in OCaml RETURNS VALUES**

#### 1.2 `Let` can be nested
The let syntexs should be read inside out.
	1.  let a = 3 in a+b : int -> int = \<func\> ---> b+3
	2. let b = 2 in b+3 ---> 5
	3. let a = 1 in a+5 ---> 6 // stop
```ocaml
# let a = 1 in
  a + let b = 2  in
		    let a = 3 in
		      a+b;;
- : int = 6
```

#### 1.3 Full nested scope and Shadowing
1. Full nested scope: nested `let` establishes bindings that variables are all visible in the innermost `let` body
	- ex: let b = 2 is visible in a+b, to search for b, the innermost body will expand the searching to the nearest bound.(a=3 -> b =2)
2. shadowing: a name has a previous binding (in an outer scope) and you bind the same name again (like a in the above example); then the previous definded variable will be shadowed, and only the newest variable with the same name will be available
	- ex: when find (a + let b = 2) syntex, (a+b) will return with given a =3, b= 2; the syntex will not keep searching the next binding.
	- The value of the first a will not be overwritten!!!


#### 1.4 `let` and functions
To declare a function `squared` with `x` its one parameter. `return` is implicit.

``` OCaml
let squared x = x * x;; 
squared 4;; (* to call a function -- separate arguments with S P A C E S *)
```

-   OCaml has no `return` statement; value of the whole body-expression is what gets returned
-   Type is automatically **inferred** and printed as domain `->` range
-   OCaml functions in fact always take only one argument - ! multiple arguments can be encoded (later)
- 
``` ad-info
Moral: re-load all your functions if you change any one function
```


## 2. Functions
#### 2.1 How to define a function
##### Single Argument Function (lambda expression)
``` ocaml
let add1 x = x + 1;; (* a normal add1 definition *)
let anon_add1 = (function x -> x + 1);; (* equivalent anonymous version; "x" is argument here *)
let anon_add1_fun = (fun x -> x + 1);; (* `function` can usually be shortened to `fun` *)

((fun x -> x + 1) 4) * 7;; (* can inline anonymous function definition; makes no sense here but will later *)

```
##### Multiple Arguments Functions
``` ocaml
# let add x y = x + y;;
val add : int -> int -> int = <fun>
add 3 4;;
(add 3) 4;; (* same meaning as previous application -- two applications, " " associates LEFT *)
```
- `add` is in fact a **higher-order function**: it returns a function as result.
- Observe `int -> int -> int` is parenthesized as `int -> (int -> int)` – unusual **right** associativity

``` ocaml

let add3 = add 3;; (* No need to give all arguments at once!  Type of add is int -> (int -> int) - "CURRIED" *)
# add3 4;;
- : int = 7
```
- Partial application: any infix operator in OCaml can also be used as a prefix operator by surrounding the operator with parens and putting it in a prefix position, like so: 2 + 3 = (+) 2 3.

``` ocaml
(+) 3 4;; (* Putting () around any infix operator turns it into a 2-argument function: `(+)` is same as our `add` above *)

# List.map ((+) 2) [1;2;3];; (* More Convenient! *)
- : int list = [3; 4; 5]

```
Take care of the precedence!!

```ocaml
# add3 (2*3);;
- : int = 9

# add3 2*3;;
- : int = 15 (* NOT the previous - this is the same as (add3 3) * 2 - application binds tighter than `*` *)

# add3 @@ 2*3;;
- : int = 9 (* LIKE the original - @@ is like the " " for application but binds LOOSER than other ops *)
```

This is in fact how OCaml functions work: every function of n > 1 arguments is actually a function of one argument that returns a function of n-1 arguments as its result. OCaml's function-application syntax, combined with the left-associativity of function application, makes this transparent.

Here are three identical defination: 
``` ocaml
# let f1 = fun a -> fun b -> fun c -> a + b * c;;
val f1 : int -> int -> int -> int = <fun>

# let f2 = fun a b c -> a + b * c;;
val f2 : int -> int -> int -> int = <fun>

# let f3 a b c = a + b * c;;
val f3 : int -> int -> int -> int = <fun>
```

So, what is the value of `f1 6`? It's `6+b+c`, because the first function accepts paramater `a`

#### Declaring types in OCaml
While OCaml infers types for you it is often good practice to add those types to your code, e.g.

``` ocaml
let add (x : int) (y : int) : int = x + y;;
```

Note that the parentheses here are required, and the return type is at the end.
#### 2.2 How to call a function
- No paranthesis is needed
``` ocaml
# let add1 x = x + 1;;
val add1 : int -> int = <fun>

# add1 3;;(* Call Function *)
- : int = 4
```

#### 2.3 `let rec` Recursion
``` ocaml
let rec fib n =     (* the "rec" keyword needs to be added to allow recursion *)
  if n <= 0 then 0
  else if n = 1 then 1
  else fib (n - 1) + fib (n - 2);; (* notice again everything is an expression, no "return" *)

fib 10;; (* get the 10th Fibonacci number *)

```


#### 2.4 `let rec...and`  Mutually recursive functions
- Sometimes, we wanna let two recursions call each other in their definations. Now is a good time to use keyword `and` to chain together any number of definations, although, in general, definitions must occur before use

Here's a nice definition of a pair of functions
- `take`, which returns every other element of its list argument starting with the first,
- `skip`, which also returns every other element but starts with the second (skipping the first). 
They can be conveniently defined in terms of each other:

``` ocaml
# let rec take list =
	  if list = []
	  then []
	  else List.hd list :: skip (List.tl list) (* call `skip` in `take` *)
	  (* List.hd return the first element *)
	and skip list =
	  if list = []
	  then []
	  else take (List.tl list) (* List.tl return elements except the first one*)
      ;;
val take : 'a list -> 'a list = <fun>
val skip : 'a list -> 'a list = <fun>


# take [1;2;3;4;5;6;7];;
- : int list = [1; 3; 5; 7]

# skip [1;2;3;4;5;6;7];;
- : int list = [2; 4; 6]


```
Here it is another identical defination
``` ocaml
let rec copy_odd l = match l with
  | [] -> []
  | hd :: tl ->  hd::(copy_even tl) (* The same as `take`, take the first element*)
and  (* new keyword for declaring mutually recursive functions *)
  copy_even l = match l with
  |  [] -> []
  | x :: xs -> copy_odd xs;; (* The same as skip, skip the first element*)

copy_odd [1;2;3;4;5;6;7;8;9;10];;
copy_even [1;2;3;4;5;6;7;8;9;10];;

```
#### 2.5  Using `let .. in` to define local functions

-   If functions are _only_ used locally within one function, it can be defined inside that function - more modular
-   Suppose we only wanted to use `copy_odd`: here is a version that hides `copy_even`:
- It is similar to the basic usage of `let` in variable condition, which read in a right-first goofy order.
``` ocaml
 let name = expr1 in expr2;; (* name is local to expr2*)
 let n = 2 in n*n;;
```

``` ocaml
# let copy_odd ll =
  let rec copy_odd_local l = match l with (* mutually defination *)
    |  [] -> []
    | hd :: tl ->  hd::(copy_even_local tl)
  and
    copy_even_local l = match l with
    |        [] -> []
    | x :: xs -> copy_odd_local xs
  in (*local defination*)
  copy_odd_local ll;; (* call copy_odd_local with parameter given in the top lool, and copy_odd_local is defined above *)
val copy_odd : 'a list -> 'a list = <fun>

assert(copy_odd [1;2;3;4;5;6;7;8;9;10] = [1;3;5;7;9]);;
```

-   `copy_even_local` is not available in the top loop, it is local to `copy_odd` function only, just like local variables but its a function.
-   Note how the last line “exports” the internal `copy_odd_local` by forwarding the `ll` parameter to it
#### 2.6 Higher Order Functions

Higher order functions are functions that either

-   **take other functions as arguments**
-   or **return functions as results**

Why?

-   “pluggable” programming by passing in and out chunks of code
-   greatly increases reusability of code since any varying code can be pulled out as a function to pass in
-   Lets show the power by extracting out some pluggable code

Illustration of there usefulness by example: append `"gobble"` to each word in a list of strings

```ocaml
let rec append_gobble l =
  match l with
  | [] -> []
  | hd::tl -> (hd ^"-gobble") :: append_gobble tl;;

append_gobble ["have";"a";"good";"day"];;
("have" ^"gobble") :: ("a"^"gobble") :: append_gobble ["good";"day"];;
```

-   At a high level, the common pattern is “apply a function to every list element and make a list of the results”
-   So, lets pull out the “append gobble” action as a function parameter that we can plug in
-   The resulting function is called `map` (note it is built-in as `List.map`): map a function to every element in List
    
    ``` ocaml
    let rec map (f : 'a -> 'b) (l : 'a list) : 'b list =  (* function f is an argument here *)
    match l with
    | [] -> []
    | hd::tl -> (f hd) :: map f tl;;
    ```
    

``` ocaml
let another_append_gobble = map (fun s -> s^"-gobble");; (* give only the first argument -- Currying *)
another_append_gobble ["have";"a";"good";"day"];;
map (fun s -> s^"-gobble") ["have";"a";"good";"day"];; (* Or, don't give the intermediate application a name *)
```

Mapping on lists of pairs - in and out lists can be different types.
Here is an example to return a list of functions

``` ocaml
# List.map (fun (x,y) -> x + y) [(1,2);(3,4)];;
- : int list = [3; 7]

# let flist = List. map (fun x -> (fun y -> x + y)) [1;2;4] ;;
val flist : (int -> int) list = [<fun>; <fun>; <fun>]
(* make a list of functions - why not? *)
```

